plugins {
    id 'org.springframework.boot' version '2.6.1'
    id 'io.spring.dependency-management' version '1.0.11.RELEASE'
    id 'java-library'
    id 'maven-publish'
    id 'signing'
    id 'idea'
}

group = 'ltd.clearsolutions'
version = '0.0.2-SNAPSHOT'
sourceCompatibility = '17'

java {
    withJavadocJar()
    withSourcesJar()
}

repositories {
    mavenCentral()
}
bootJar {
    enabled = false
}
jar {
    enabled = true
}

ext {
    httpclientVersion = '4.5.13'
    commonsloggingVersion = '1.2'
    jacksonVersion = '2.13.1'
    slf4jVersion = '1.7.32'
    logbackVersion = '1.2.10'
    junitVersion = '5.8.2'
    assertjVersion = '3.21.0'
    wiremockVersion = '2.32.0'
}

dependencies {
    implementation "org.apache.httpcomponents:httpclient:${httpclientVersion}"
    implementation "commons-logging:commons-logging:${commonsloggingVersion}"
    implementation "com.fasterxml.jackson.core:jackson-annotations:${jacksonVersion}"
    implementation "com.fasterxml.jackson.core:jackson-core:${jacksonVersion}"
    implementation "com.fasterxml.jackson.core:jackson-databind:${jacksonVersion}"
    implementation "com.fasterxml.jackson.dataformat:jackson-dataformat-xml:${jacksonVersion}"
    implementation "com.fasterxml.jackson.core:jackson-databind:${jacksonVersion}"

    implementation "org.slf4j:slf4j-api:${slf4jVersion}"
    implementation "ch.qos.logback:logback-core:${logbackVersion}"
    implementation "ch.qos.logback:logback-classic:${logbackVersion}"

    testImplementation(platform("org.junit:junit-bom:${junitVersion}"))
    testImplementation "org.junit.jupiter:junit-jupiter"
    testImplementation "org.assertj:assertj-core:${assertjVersion}"
    testImplementation "com.github.tomakehurst:wiremock-jre8:${wiremockVersion}"
}

test {
    useJUnitPlatform()
    testLogging {
        events "passed", "skipped", "failed"
        exceptionFormat = 'full'
    }
}

publishing {
    repositories {
        maven {
            name = "GitHubPackages"
            url = uri("https://maven.pkg.github.com/clear-solutions/lingualeo-client")
            credentials {
                username = project.findProperty("gpr.user") ?: System.getenv("USERNAME")
                password = project.findProperty("gpr.key") ?: System.getenv("TOKEN")
            }
        }
    }
    publications {
        gpr(MavenPublication) {
            from(components.java)
            versionMapping {
                usage('java-api') {
                    fromResolutionOf('runtimeClasspath')
                }
                usage('java-runtime') {
                    fromResolutionResult()
                }
            }
        }
    }
}
